import org.jetbrains.gradle.ext.Gradle
import org.jetbrains.intellij.tasks.RunPluginVerifierTask

plugins {
    id 'org.jetbrains.intellij' version '0.6.1'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id "org.jetbrains.gradle.plugin.idea-ext" version "0.7"
}

group 'ru.curs.celesta.intellij'
version '0.1-SNAPSHOT'

ext.verifierIdeVersions = ["IU-2020.1.4", "IU-2020.2.3", "IU-2020.3"]

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

idea {
    project {
        settings {
            runConfigurations {
                "Run Plugin"(Gradle) {
                    projectPath = projectDir.absolutePath
                    taskNames = [":runIde"]
                }
                "Build Plugin"(Gradle) {
                    projectPath = projectDir.absolutePath
                    taskNames = [":buildPlugin"]
                }
            }
        }
    }
}

intellij {
    version '2020.2.3'
    type 'IU'
    plugins = ['PersistenceSupport', 'DatabaseTools', 'JPA', 'java', 'maven', 'maven-model']
}

runPluginVerifier {
    ideVersions = verifierIdeVersions
    failureLevel = EnumSet.copyOf([RunPluginVerifierTask.FailureLevel.INVALID_PLUGIN, RunPluginVerifierTask.FailureLevel.COMPATIBILITY_PROBLEMS])
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

patchPluginXml {
    changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}