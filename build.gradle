import org.jetbrains.gradle.ext.Gradle
import org.jetbrains.intellij.tasks.RunPluginVerifierTask

plugins {
    id "java"
    id "org.jetbrains.intellij" version "1.13.3"
    id "org.jetbrains.kotlin.jvm" version "1.8.20"
}

group "ru.curs.celesta.intellij"
version "1.03"

ext.verifierIdeVersions = ["IU-2023.1.1"]

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.32"
}

idea {
    project {
        settings {
            runConfigurations {
                "Run Plugin"(Gradle) {
                    projectPath = projectDir.absolutePath
                    taskNames = [":runIde"]
                }
                "Build Plugin"(Gradle) {
                    projectPath = projectDir.absolutePath
                    taskNames = [":buildPlugin"]
                }
            }
        }
    }
}

intellij {
    version.set("2023.1.1")
    type.set("IU")
    plugins = ["DatabaseTools", "JPA", "java", "maven", "maven-model"]
}

runPluginVerifier {
    ideVersions = verifierIdeVersions
    failureLevel = EnumSet.copyOf([RunPluginVerifierTask.FailureLevel.INVALID_PLUGIN, RunPluginVerifierTask.FailureLevel.COMPATIBILITY_PROBLEMS])
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "17"
}

patchPluginXml {
    changeNotes = new File("changelog.html").getText("UTF-8")
    sinceBuild = "231"
    untilBuild = "231.*"
}
